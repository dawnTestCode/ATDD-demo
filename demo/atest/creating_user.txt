*** Test Cases ***

Users can create a new account with a username and password
    Given there are no accounts in the system
    When a user creates an account with username "fred" and password "password"
    Then the system responds with the message "Account created"


Users can login with a valid account
    Given there is an account with username "fred" and password "password"
    When a user attempts to login with username "fred" and password "password"
    Then the system responds with the message "Welcome!"


Account information is stored in a database
    Given there is an account with username "fred" and password "password"
    Then the database contains an active account fred/password


Users cannot login with invalid password
    Given there is an account with username "fred" and password "password"
    When a user attempts to login with username "fred" and password "bad"
    Then the system responds with the message "Access denied"
    

*** Keywords ***

Given there are no accounts in the system
    Remove File  ${TEMPDIR}/pwds.txt


When a user creates an account with username "${username}" and password "${password}"
    ${STATUS} =  Create User  ${username}  ${password}      
    Set Test Variable  ${STATUS}


Then the system responds with the message "${message}"
    Should Be Equal  ${STATUS}  ${message}


Given there is an account with username "${username}" and password "${password}"
    Given there are no accounts in the system
    When a user creates an account with username "${username}" and password "${password}"
    Then the system responds with the message "Account created"


When a user attempts to login with username "${username}" and password "${password}"
    ${STATUS} =  Attempt to Login  ${username}  ${password}
    Set Test Variable  ${STATUS}
    

Then the database contains an ${state} account ${username}/${passwor}
    ${db} =  Get File  ${DATABASE}
    Should Contain  ${db}  ${username}\t${password}\t${state}



*** Settings ***

Library  OperatingSystem
Library  ${DRIVER}


*** Variables ***
${DRIVER}    CliLibrary.py
${DATABASE}  ${TEMPDIR}/pwds.txt
