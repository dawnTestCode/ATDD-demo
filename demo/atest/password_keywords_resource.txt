




*** Keywords ***

Given there are no accounts in the system
    Remove File  ${DATABASE}
    Reload User Database

When a user creates an account with username "${username}" and password "${password}"
    ${STATUS} =  Create User  ${username}  ${password}      
    Set Test Variable  ${STATUS}

When a user creates a password with no number
    ${STATUS} =  Create User  fred123  p@ssword      
    Set Test Variable  ${STATUS}
   
When a user creates a password with no number and no symbol
    ${STATUS} =  Create User  fred123  password      
    Set Test Variable  ${STATUS}

Then the system responds with the message "${message}"
    Should Be Equal  ${STATUS}  ${message}

When a user attempts to create an account with a username that already exists
    Create User  fred  p@ssw0rd
    ${STATUS} =  Create User  fred  p@ssw1rd
    Set Test Variable  ${STATUS}

When a user attempts to create an account with an invalid password
    ${STATUS} =  Create User  fred  bad
    Set Test Variable  ${STATUS}

Given there is an account with username "${username}" and password "${password}"
    Given there are no accounts in the system
    When a user creates an account with username "${username}" and password "${password}"
    Then the system responds with the message "Account created"

When a user attempts to login with username "${username}" and password "${password}"
    ${STATUS} =  Attempt to Login  ${username}  ${password}
    Set Test Variable  ${STATUS}
    
Then the database contains an ${state} account ${username}/${password}
    ${db} =  Get File  ${DATABASE}
    Should Contain  ${db}  ${username}\t${password}\t${state}

Given there is an account with username "${username}" and invalid password "${password}"
    Create File  ${DATABASE}  ${username}\t${password}\tactive\n
    Reload User Database

are invalid  [Arguments]  ${password}
    Given there are no accounts in the system
    When a user creates an account with username "fred" and password "${password}"
    Then the system responds with the message "The submitted password does not meet the password requirements."

are valid  [Arguments]  ${password}
    Given there are no accounts in the system
    When a user creates an account with username "fred" and password "${password}"
    Then the system responds with the message "Account created"

When a user attempts to create an account using the same password as their username
    Given there are no accounts in the system
    ${STATUS} =  Create User  fred!0  fred!0
    Set Test Variable  ${STATUS}

If a user attempts to create an account with a too-long password by directly posting data the system responds with the invalid password message
    No Operation

*** Settings ***

Library         OperatingSystem
Resource        ${INTERFACE}_resource.txt


*** Variables ***

${INTERFACE}  cli
${DATABASE}   ${TEMPDIR}/pwds.txt